apply plugin: "java"
apply plugin: "maven"

group "com.github.kinguinltdhk"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases'
    }
}

dependencies {
    compile "com.ecwid.consul:consul-api:1.2.3"
    compile "com.fasterxml.jackson.core:jackson-core:2.9.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.0"
    compile "io.reactivex:rxjava:1.3.2"
    compile "com.squareup.okhttp3:okhttp:3.12.0"
    compileOnly "org.projectlombok:lombok:1.18.4"

    testCompile "com.github.tomakehurst:wiremock:2.19.0"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.23.4"
    testCompile "org.mockito:mockito-inline:2.23.4"
    testCompile "com.palantir.docker.compose:docker-compose-rule-junit4:0.32.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnit {
        excludeCategories 'net.kinguin.leadership.consul.IntegrationTest'
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'net.kinguin.leadership.consul.IntegrationTest'
    }
}


// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}